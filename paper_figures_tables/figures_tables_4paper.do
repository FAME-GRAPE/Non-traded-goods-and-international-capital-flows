/*
Stata .do file for "Non-traded goods, factor market frictions, and
international capital flows", Jacek Rothert and Jacob Short.

THIS .do FILE IMPORTS THE OBSERVED DATA AND THE MODEL DATA FROM THE CALIBRATION AND COUNTERFACTUALS 
AND THEN GENERATES THE FIGURES AND TABLES FOR THE PAPER "" 

NOTE: THIS FILE REQUIRES THAT THE FILE "output4paper_figures_tables.m" BE RUN IN MATLAB 
PRIOR TO RUNNING THIS .do FILE.

INPUT FILES:
1. modeldata_4stata.xlsx - contains cross-sectional data from observed data, model generated data, and counterfactuals
2. LNtimeseries_bench.xlsx - contains time series for labor from benchmark model
3. LNtimeseries_psiL.xlsx - contains time series for labor from model without factor market frictions
4. LNtimeseries_tauL.xlsx - contains time series for labor from model without labor market frictions
5. data_laborflows_timeseries.dta - contains time series for labor and value added from observed data

NOTE: Input files 1-4 are generated by running the post-calibration output_4paper.m file in matlab, file 5
is the full time series data of labor and value added for each country.

OUTPUT:
1. figures for paper in .png format (stored in figpath folder)
2. tables for paper in .tex format (stored in tablepath, tables_4paper.tex)


*/
clear all

*SET FOLDER CONTAINING THE OBSERVED AND MODEL DATA
global resultspath "{INSERT PATH CONTAINING DATA, CALIBRATION, AND COUNTERFACTUAL OUTPUT FROM MATLAB}"

*SET PATH FOR OUTPUT OF FIGURES AND TABLES (FOLDERS "figures" AND "tables" NEED TO BE CREATED IN THE RESPECTIVE PATHS)
global figpath "{INSERT PATH FOR OUTPUT OF FIGURES}"
global tablepath "{INSERT PATH FOR OUTPUT OF TABLES}"

*Import Observed, Calibrated, and Counterfactuals data
import excel "$resultspath\modeldata_4stata.xlsx", sheet("Sheet1") firstrow


/************************
GENERATE SOME VARIABLES THAT WILL BE USED
*/

gen numcntrya=1
egen numcntry=sum(numcntrya)
drop numcntrya


*convert data into percentages
gen dydata_perc=dy_data*100
gen dpdata_perc=drer_data*100
gen iovery_perc=IoverY_data*100


gen dybench_perc=dy_bench*100
gen dpbench_perc=drer_bench*100
gen ioverybench_perc=IoverY_bench*100


gen dLNT_data=(LNT_data-LN0_data)/(TT)

gen rel_gTgN=gT_bench/gN_bench

*generate model labor relative to initial labor in the data

gen peakLN0psiL_bench=max(LN0psiL_bench, LN1psiL_bench, LN2psiL_bench)

gen relpeakLN0psiL_bench=peakLN0psiL_bench/LN0_data

gen deltaLN_data=LNT_data-LN0_data
gen deltaLNpsiL_bench=LNTpsiL_bench-peakLN0psiL_bench

gen avg_deltaLNpsiL_bench=deltaLNpsiL_bench/TT
gen avg_deltaLN_data=deltaLN_data/TT


gen tauSLess1_bench=(-1)*(tauS_bench-1)
gen tauSLess1_1sec=(-1)*(tauS_1sec-1)


/*
save "$resultspath\crosscountry_data_model_output.dta", replace
*/


/*
GENERATE FIGURES FOR THE PAPER
*/


***************
* FIGURE 1 - CA Flows v dy from data
***************

*ONE SECTOR V DATA 
twoway (scatter DDY0_data dydata_perc, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (scatter DDY0tauS_1sec dydata_perc, sort mcolor(blue)), /*
*/ ytitle("Cumulative capital inflows (rel. to initial GDP)") xtitle("Real GDP Growth (%)") /*
*/ title("") graphregion(fcolor(white))/*
*/ legend(order(2 "Flows in the one-sector model") position(12) ring(0))
graph export "$figpath\figure1_DDY0_v_dy_1sector.png", as(png) name("Graph") replace




***************
* FIGURE 2 - Total Private CA Flows v dy from data
***************

**Capital Flows v GDP growth without the fit or gstar line line
twoway (scatter DDY0_data dydata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)), /*
*/ ytitle("{&Delta}D/GDP") xtitle("") title("Total Flows") legend(off) graphregion(fcolor(white)) 
graph save "Graph" "$figpath\figure2a_DDY0data_v_dydata.gph", replace

**Private Capital Flows v GDP growth without the fit line
twoway (scatter DDY0priv_data dydata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)), /*
*/ ytitle("{&Delta}D/GDP") xtitle("Real GDP Growth (%)") title("Private Flows") legend(off) graphregion(fcolor(white)) 
graph save "Graph" "$figpath\figure2b_DDY0priv_v_dydata.gph", replace


graph combine "$figpath\figure2a_DDY0data_v_dydata.gph" /*
*/ "$figpath\figure2b_DDY0priv_v_dydata.gph", /*
*/ title("") graphregion(fcolor(white)) rows(2)
graph export "$figpath\figure2_DDY0flows_v_dydata.png", as(png) name("Graph") replace




***************
* FIGURE 3 - model fit
***************

twoway (scatter dybench_perc dydata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (line dydata_perc dydata_perc), ytitle(Model) /*
*/ xtitle(Data) title("Real GDP Growth (%)") legend(off) graphregion(fcolor(white))
graph save "Graph" "$figpath\dyfit_perc.gph", replace

twoway (scatter dpbench_perc dpdata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (line dpdata_perc dpdata_perc), ytitle(Model) /*
*/ xtitle(Data) title("Changes in RER (%)") legend(off) graphregion(fcolor(white))
graph save "Graph" "$figpath\drerfit_perc.gph", replace

twoway (scatter ioverybench_perc iovery_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (line iovery_perc iovery_perc), ytitle(Model) /*
*/ xtitle(Data) title("Avg. Investment to GDP Ratio (%)") legend(off) graphregion(fcolor(white))
graph save "Graph" "$figpath\ioveryfit_perc.gph", replace

twoway (scatter DDY0_bench DDY0_data, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (line DDY0_data DDY0_data), ytitle(Model) /*
*/ xtitle(Data) title("Capital Flows (relative to initial GDP)") legend(off) graphregion(fcolor(white))
graph save "Graph" "$figpath\ddy0fit.gph", replace
graph export "$figpath\ddy0fit.png", replace

graph combine "$figpath\dyfit_perc.gph" /*
*/ "$figpath\drerfit_perc.gph" /*
*/ "$figpath\ioveryfit_perc.gph" /*
*/ "$figpath\ddy0fit.gph", title("Model Fit - Benchmark") graphregion(fcolor(white))
graph export "$figpath\figure3_modelfit_bench_percent.png", as(png) name("Graph") replace




***************
* FIGURE 4 - Capital Inflows v dy - data, 1 sector tauS, two sector tauS and psiL
***************

*ONE SECTOR, TWO SEC without International Frictions V DATA 
twoway (scatter DDY0_data dydata_perc, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (scatter DDY0tauS_1sec dydata_perc, sort mcolor(blue) ) /*
*/ (scatter DDY0tauS_bench dydata_perc, sort msymbol(diamond_hollow) mcolor(green)), /*
*/ ytitle("Cumulative capital inflows (relative to initial GDP)") xtitle("Real GDP Growth (%)") /*
*/ title("") graphregion(fcolor(white)) yline(0, lcolor(black)) xline(2.56, lcolor(black)) /*
*/ legend(off)
graph export "$figpath\figure4a_DDY0_v_dy_1sec2sec_tauD.png", as(png) name("Graph") replace

*TWO SEC without Domestic Frictions V DATA 
twoway (scatter DDY0_data dydata_perc, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (scatter DDY0tauS_bench dydata_perc, sort msymbol(diamond_hollow) mcolor(green)) /*
*/ (scatter DDY0psiL_bench dydata_perc, sort msymbol(lgx) mcolor(red)), /*
*/ ytitle("Cumulative capital inflows (relative to initial GDP)") xtitle("Real GDP Growth (%)") /*
*/ title("") graphregion(fcolor(white)) yline(0, lcolor(black)) xline(2.56, lcolor(black)) /*
*/ legend(off)
graph export "$figpath\figure4b_DDY0_v_dy_only2sector_psiLtauD.png", as(png) name("Graph") replace




***************
* FIGURE 5 - Counterfactual Labor movements
***************

twoway (scatter relpeakLN0psiL_bench rel_gTgN, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (lfit relpeakLN0psiL_bench rel_gTgN, sort ) if iso~="PER", /*
*/ yline(0,lcolor(black)) ytitle("Non-tradable employment (initial period, rel. to data)") /*
*/ xtitle("Relative TFP growth (g{superscript:T}/g{superscript:N})") title("Initial labor reallocation") /*
*/ legend(off) graphregion(fcolor(white)) 
graph export "$figpath\figure5a_relLN0_v_relgTgN_psiL.png", as(png) name("Graph") replace

twoway (scatter avg_deltaLNpsiL_bench avg_deltaLN_data, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (lfit avg_deltaLNpsiL_bench avg_deltaLN_data, sort ), /*
*/ ylabel(-0.02(.01)0.01)  /*
*/ yline(0,lcolor(black)) xline(0,lcolor(black)) ytitle("{&Delta} employment share - non-tradable (model)") /*
*/ xtitle("{&Delta} employment share - non-tradable (data)") title("Long-run labor reallocation") /*
*/ legend(off) graphregion(fcolor(white)) 
graph export "$figpath\figure5b_avgdeltaLN_v_avgdeltaLNdata_psiL.png", as(png) name("Graph") replace

twoway (scatter DDY0psiL_bench relpeakLN0psiL_bench, sort msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ (lfit DDY0psiL_bench relpeakLN0psiL_bench, sort ) if iso~="SGP", /*
*/ xtitle("Non-tradable employment (initial period, rel. to data)") ytitle("Cumulative capital inflows") /*
*/ title("") legend(off) graphregion(fcolor(white)) 
graph export "$figpath\figure5c_DDY0_v_relLN0_psiL.png", as(png) name("Graph") replace




***************
* FIGURE 6 - tauS v dy from two sector benchmark
***************

twoway (scatter tauSLess1_1sec dydata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ if iso~="PER", /*
*/ ytitle("Debt Price Wedge, {&tau}{superscript:D}") xtitle("Real GDP Growth (%)") title("") legend(off) graphregion(fcolor(white)) yline(0, lcolor(black)) xline(2.56, lcolor(black))
graph export "$figpath\figure6a_tauD_v_dydata_1sec_noPER.png", replace

twoway (scatter tauSLess1_bench dydata_perc, msymbol(none) mlabel(iso) mlabcolor(black) mlabposition(0)) /*
*/ if iso~="PER", /*
*/ ytitle("Debt Price Wedge, {&tau}{superscript:D}") xtitle("Real GDP Growth (%)") title("") legend(off) graphregion(fcolor(white)) yline(0, lcolor(black)) xline(2.56, lcolor(black))
graph export "$figpath\figure6b_tauD_v_dydata_2sec_noPER.png", replace




/*
********************************************************

CREATE OUTPUT FOR TABLES IN THE PAPER

********************************************************
*/


***************
* TABLE 1 - ACTUAL VS. PREDICTED SIZE OF FLOWS
***************

eststo: mean dy_data DDY0_data DDY0tauS_1sec if iso=="KOR"
eststo: mean dy_data DDY0_data DDY0tauS_1sec if iso=="CHN"
eststo: mean dy_data DDY0_data DDY0tauS_1sec if iso=="IDN"
eststo: mean dy_data DDY0_data DDY0tauS_1sec if iso=="HND"
eststo: mean dy_data DDY0_data DDY0tauS_1sec if iso=="ITA"

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 1: DATA and ONE-SECTOR Flows") /*
 */ replace b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("Korea" "China" "India" "Honduras" "Italy") /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear




***************
* TABLE 4 - MAIN RESULTS	
***************

**TWO-SECTOR
gen DDY0sqerr_bencha=(DDY0_data-DDY0_bench)^2
egen DDY0sqerr_bench=sum(DDY0sqerr_bencha)
replace DDY0sqerr_bench=DDY0sqerr_bench/numcntry
label var DDY0sqerr_bench "SSE Bench"

gen DDY0sqerr_benchtauSa=(DDY0_data-DDY0tauS_bench)^2
egen DDY0sqerr_benchtauS=sum(DDY0sqerr_benchtauSa)
replace DDY0sqerr_benchtauS=DDY0sqerr_benchtauS/numcntry
label var DDY0sqerr_benchtauS "SSE Bench-tauD"

gen DDY0sqerr_benchtauSKa=(DDY0_data-DDY0tauSK_bench)^2
egen DDY0sqerr_benchtauSK=sum(DDY0sqerr_benchtauSKa)
replace DDY0sqerr_benchtauSK=DDY0sqerr_benchtauSK/numcntry
label var DDY0sqerr_benchtauSK "SSE Bench-tauD tauK"

gen DDY0sqerr_benchpsiLa=(DDY0_data-DDY0psiL_bench)^2
egen DDY0sqerr_benchpsiL=sum(DDY0sqerr_benchpsiLa)
replace DDY0sqerr_benchpsiL=DDY0sqerr_benchpsiL/numcntry
label var DDY0sqerr_benchpsiL "SSE Bench-psiL"

gen DDY0sqerr_benchpsiSKLa=(DDY0_data-DDY0psiSKL_bench)^2
egen DDY0sqerr_benchpsiSKL=sum(DDY0sqerr_benchpsiSKLa)
replace DDY0sqerr_benchpsiSKL=DDY0sqerr_benchpsiSKL/numcntry
label var DDY0sqerr_benchpsiSKL "SSE Bench-psiSKL"

gen DDY0sqerr_benchpsia=(DDY0_data-DDY0psi_bench)^2
egen DDY0sqerr_benchpsi=sum(DDY0sqerr_benchpsia)
replace DDY0sqerr_benchpsi=DDY0sqerr_benchpsi/numcntry
label var DDY0sqerr_benchpsi "SSE Bench-psi"

gen DDY0sqerr_benchtauLa=(DDY0_data-DDY0tauL_bench)^2
egen DDY0sqerr_benchtauL=sum(DDY0sqerr_benchtauLa)
replace DDY0sqerr_benchtauL=DDY0sqerr_benchtauL/numcntry
label var DDY0sqerr_benchtauL "SSE Bench-tauL"

**ONE-SECTOR
gen DDY0sqerr_1seca=(DDY0_data-DDY0_1sec)^2
egen DDY0sqerr_1sec=sum(DDY0sqerr_1seca)
replace DDY0sqerr_1sec=DDY0sqerr_1sec/numcntry
label var DDY0sqerr_1sec "SSE 1sec"

gen DDY0sqerr_1sectauSa=(DDY0_data-DDY0tauS_1sec)^2
egen DDY0sqerr_1sectauS=sum(DDY0sqerr_1sectauSa)
replace DDY0sqerr_1sectauS=DDY0sqerr_1sectauS/numcntry
label var DDY0sqerr_1sectauS "SSE 1sec-tauD"

gen DDY0sqerr_1sectauSKa=(DDY0_data-DDY0tauSK_1sec)^2
egen DDY0sqerr_1sectauSK=sum(DDY0sqerr_1sectauSKa)
replace DDY0sqerr_1sectauSK=DDY0sqerr_1sectauSK/numcntry
label var DDY0sqerr_1sectauSK "SSE 1sec-tauD tauK"

**DATA
eststo: mean DDY0_data

sum DDY0_data, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0_data dy_data
estadd scalar DDY0correl=r(rho)

sum maxdLN_bench, detail
estadd scalar maxdLN=r(p99)

correl SoverY_data IoverY_data
estadd scalar SIcorrel=r(rho)

*private flows
eststo: mean DDY0priv_data
sum DDY0priv_data, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0priv_data dy_data
estadd scalar DDY0correl=r(rho)

**ONE-SECTOR

*one-sector calibrated
eststo: mean DDY0sqerr_1sec
sum DDY0_1sec, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0_1sec dy_1sec
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_1seca, detail
estadd scalar DDY0sqerr_median=r(p50)

correl SoverY_1sec IoverY_1sec
estadd scalar SIcorrel=r(rho)


**ONE-SECTOR without international frictions
eststo: mean DDY0sqerr_1sectauS
sum DDY0tauS_1sec, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0tauS_1sec dytauS_1sec
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_1sectauSa, detail
estadd scalar DDY0sqerr_median=r(p50)

correl SoverYtauS_1sec IoverYtauS_1sec
estadd scalar SIcorrel=r(rho)

**ONE-SECTOR without international and investment frictions
eststo: mean DDY0sqerr_1sectauSK
sum DDY0tauSK_1sec, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0tauSK_1sec dytauSK_1sec
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_1sectauSKa, detail
estadd scalar DDY0sqerr_median=r(p50)

correl SoverYtauSK_1sec IoverYtauSK_1sec
estadd scalar SIcorrel=r(rho)


esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 4A, Main Results: DATA and ONE-SECTOR") /*
 */ append b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("Data" "Private" "one-sector" "one-sector without $\tau^D$" "one-sector no $\tau^D$ $\tau^K$") /*
 */ stats(p50 DDY0min DDY0max DDY0correl DDY0sqerr_median maxdLN SIcorrel N, label("Statistics" /*
 */ "Min $\Delta$ D" "Max $\Delta$ D" "$\rho(\frac{\Delta D}{Y_0},\Delta GDP)$" /*
 */ "Median $\Delta$ D sq. err" "max dLNT" "SI correl" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear


**TWO-SECTOR
*two-sector benchmark
eststo: mean DDY0sqerr_bench
sum DDY0_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0_bench dy_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_bencha, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLN_bench, detail
estadd scalar maxdLN=r(p99)

correl SoverY_bench IoverY_bench
estadd scalar SIcorrel=r(rho)

*two-sector without international frictions
eststo: mean DDY0sqerr_benchtauS
sum DDY0tauS_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0tauS_bench dytauS_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_benchtauSa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLN_bench, detail
estadd scalar maxdLN=r(p99)

correl SoverYtauS_bench IoverYtauS_bench
estadd scalar SIcorrel=r(rho)

*two-sector without international and investment frictions
eststo: mean DDY0sqerr_benchtauSK
sum DDY0tauSK_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0tauSK_bench dytauSK_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_benchtauSKa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLN_bench, detail
estadd scalar maxdLN=r(p99)

correl SoverYtauSK_bench IoverYtauSK_bench
estadd scalar SIcorrel=r(rho)

*two-sector without domestic factor market frictions
eststo: mean DDY0sqerr_benchpsiL
sum DDY0psiL_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0psiL_bench dypsiL_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_benchpsiLa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLNpsiL_bench, detail
estadd scalar maxdLN=r(p99)

correl IoverYpsiL_bench SoverYpsiL_bench
estadd scalar SIcorrel=r(rho)

*two-sector without international and domestic factor market frictions
eststo: mean DDY0sqerr_benchpsiSKL
sum DDY0psiSKL_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0psiSKL_bench dypsiSKL_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_benchpsiSKLa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLNpsiSKL_bench, detail
estadd scalar maxdLN=r(p99)

correl IoverYpsiSKL_bench SoverYpsiSKL_bench
estadd scalar SIcorrel=r(rho)


esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 4B, Main Results: TWO-SECTOR") /*
 */ append b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("two-sector" "two-sector w/o $\tau^D$" "two-sector no $\tau^D$ $\tau^K$ " /*
 */ "two-sector w/o $\tau^L$" "two-sector no frictions") /*
 */ stats(p50 DDY0min DDY0max DDY0correl DDY0sqerr_median maxdLN SIcorrel N, label("Statistics" /*
 */ "Min $\Delta$ D" "Max $\Delta$ D" "$\rho(\frac{\Delta D}{Y_0},\Delta GDP)$" /*
 */ "Median $\Delta$ D sq. err" "max dLNT" "SI correl" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear




***************
* TABLE 5 - CAPITAL FLOWS AND FACTOR MARKET FRICTIONS RESULTS	
***************

**TWO-SECTOR
*two-sector benchmark
eststo: mean DDY0sqerr_bench
sum DDY0_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

sum DDY0sqerr_bencha, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLN_bench, detail
estadd scalar maxdLN=r(p99)

*two-sector without domestic capital frictions
eststo: mean DDY0sqerr_benchpsi
sum DDY0psi_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

sum DDY0sqerr_benchpsia, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLNpsi_bench, detail
estadd scalar maxdLN=r(p99)

*two-sector without labor frictions
eststo: mean DDY0sqerr_benchtauL
sum DDY0tauL_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

sum DDY0sqerr_benchtauLa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLNtauL_bench, detail
estadd scalar maxdLN=r(p99)

*two-sector without domestic factor market frictions
eststo: mean DDY0sqerr_benchpsiL
sum DDY0psiL_bench, detail
estadd scalar DDY0min=r(p1)
estadd scalar DDY0max=r(p99)

correl DDY0psiL_bench dypsiL_bench
estadd scalar DDY0correl=r(rho)

sum DDY0sqerr_benchpsiLa, detail
estadd scalar DDY0sqerr_median=r(p50)

sum maxdLNpsiL_bench, detail
estadd scalar maxdLN=r(p99)

correl IoverYpsiL_bench SoverYpsiL_bench
estadd scalar SIcorrel=r(rho)

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 5A, Factor Market Frictions Results: TWO-SECTOR") /*
 */ append b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("two-sector" "two-sector w/o psi" "two-sector w/o $\tau^L$" /*
 */ "two-sector no domestic frictions") /*
 */ stats(p50 DDY0min DDY0max DDY0sqerr_median maxdLN N, label("Statistics" /*
 */ "Min $\Delta$ D" "Max $\Delta$ D" /*
 */ "Median $\Delta$ D sq. err" "max dLNT" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")

eststo clear





** GET TIME SERIES FOR LABOR FLOWS AND MERGE WITH DATA SERIES

clear
import excel "$resultspath\LNtimeseries_bench.xlsx", sheet("Sheet1") firstrow

rename Var1 iso

forvalues yr=2(1)71 {

rename Var`yr' LN_bench`yr'
}

reshape long LN_bench, i(iso) j(time)

replace time=time-1

save "$resultspath\model_laborflows_timeseries.dta", replace



clear
import excel "$resultspath\LNtimeseries_psiL.xlsx", sheet("Sheet1") firstrow


rename Var1 iso

forvalues yr=2(1)71 {

rename Var`yr' LN_psiL`yr'
}

reshape long LN_psiL, i(iso) j(time)

replace time=time-1


merge 1:1 iso time using "$resultspath\model_laborflows_timeseries.dta"
drop _merge

save "$resultspath\model_laborflows_timeseries.dta", replace



clear
import excel "$resultspath\LNtimeseries_tauL.xlsx", sheet("Sheet1") firstrow

rename Var1 iso

forvalues yr=2(1)71 {

rename Var`yr' LN_tauL`yr'
}

reshape long LN_tauL, i(iso) j(time)

replace time=time-1

merge 1:1 iso time using "$resultspath\model_laborflows_timeseries.dta"
drop _merge


save "$resultspath\model_laborflows_timeseries.dta", replace


merge 1:1 iso time using "$resultspath\data_laborflows_timeseries.dta"
keep if _merge==3
drop _merge


egen id=group(iso)

order id wdi_countryname iso year time t_init t_end vash_data LN_data LN_bench LN_tauL LN_psiL

tsset id year, yearly

gen dLNTy2y_data= LN_data -l.LN_data 
gen dVANTy2y_data= vash_data -l.vash_data


*purge potential measurement error in labor and value added series
gen huge_dLNTy2y=1 if abs(dLNTy2y_data)>7*abs(dVANTy2y_data) & abs(dLNTy2y_data)>0.05 & abs(dVANTy2y_data)>=.01
replace huge_dLNTy2y=1 if abs(dLNTy2y_data)>14*abs(dVANTy2y_data) & abs(dLNTy2y_data)>0.05 & abs(dVANTy2y_data)<.01

replace dLNTy2y_data=. if huge_dLNTy2y==1

*purge potential measurement error in labor and value added series
gen huge_dVANTy2y=1 if abs(dVANTy2y_data)>7*abs(dLNTy2y_data) & abs(dVANTy2y_data)>0.07 & abs(dLNTy2y_data)>=0.01
replace huge_dVANTy2y=1 if abs(dVANTy2y_data)>21*abs(dLNTy2y_data) & abs(dVANTy2y_data)>0.07 & abs(dLNTy2y_data)<0.01
*and for those we can't verify in some way
replace huge_dVANTy2y=1 if abs(dVANTy2y_data)>0.07 & dVANTy2y_data~=. & abs(dLNTy2y_data)==.

replace dVANTy2y_data=. if huge_dVANTy2y==1

*generate absolute value of changes for distribution of all year-to-year changes
gen abs_dLNTy2y=abs(dLNTy2y_data)
gen abs_dVANTy2y=abs(dVANTy2y_data)


bys id: egen min_dLNTy2y=min(dLNTy2y_data)
bys id: egen avg_dLNTy2y=mean(dLNTy2y_data)
bys id: egen med_dLNTy2y=median(dLNTy2y_data)
bys id: egen max_dLNTy2y=max(dLNTy2y_data)

bys id: egen min_dVANTy2y=min(dVANTy2y_data)
bys id: egen avg_dVANTy2y=mean(dVANTy2y_data)
bys id: egen med_dVANTy2y=median(dVANTy2y_data)
bys id: egen max_dVANTy2y=max(dVANTy2y_data)


*compute long-run changes using cross-country data model output, compute year-to-year using time series.

*compute changes in labor in model

gen TT=t_end-t_init+1

*long-run
gen LN0a=LN_data if year==t_init
gen LNTa=LN_data if year==t_end
bys id: egen LN0_data=max(LN0a)
bys id: egen LNT_data=max(LNTa)

drop LN0a LNTa

gen LN0a=LN_bench if year==t_init
gen LNTa=LN_bench if year==t_end
bys id: egen LN0_bench=max(LN0a)
bys id: egen LNT_bench=max(LNTa)

drop LN0a LNTa

gen LN0a=LN_tauL if year==t_init
gen LNTa=LN_tauL if year==t_end
bys id: egen LN0_tauL=max(LN0a)
bys id: egen LNT_tauL=max(LNTa)

drop LN0a LNTa

gen LN0a=LN_psiL if year==t_init
gen LNTa=LN_psiL if year==t_end
bys id: egen LN0_psiL=max(LN0a)
bys id: egen LNT_psiL=max(LNTa)

drop LN0a LNTa

*long-run changes
gen dLNT_data=(LNT_data-LN0_data)/TT
gen dLNT_bench=(LNT_bench-LN0_bench)/TT
gen dLNT_tauL=(LNT_tauL-LN0_tauL)/TT
gen dLNT_psiL=(LNT_psiL-LN0_psiL)/TT

gen absdLNT_data=abs(dLNT_data)
gen absdLNT_bench=abs(dLNT_bench)
gen absdLNT_tauL=abs(dLNT_tauL)
gen absdLNT_psiL=abs(dLNT_psiL)

eststo: mean dLNT_data
sum absdLNT_data, detail
estadd scalar maxdLNT=r(p99)

eststo: mean dLNT_bench
sum absdLNT_bench, detail
estadd scalar maxdLNT=r(p99)

eststo: mean dLNT_tauL
sum absdLNT_tauL, detail
estadd scalar maxdLNT=r(p99)

eststo: mean dLNT_psiL
sum absdLNT_psiL, detail
estadd scalar maxdLNT=r(p99)

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("Table 5B, Factor Market Frictions Results: LABOR ALLOCATIONS") /*
 */ append b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("data" "benchmark" "w/o $\tau^L$" /*
 */ "no domestic frictions") /*
 */ stats(p50 maxdLNT N, label("Statistics" /*
 */ "max dLNT" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear


*year-to-year
gen dLNTy2y_bench= LN_bench -l.LN_bench 
gen dLNTy2y_tauL= LN_tauL -l.LN_tauL 
gen dLNTy2y_psiL= LN_psiL -l.LN_psiL 

gen absdLNTy2y_bench=abs(dLNTy2y_bench)
gen absdLNTy2y_tauL=abs(dLNTy2y_tauL)
gen absdLNTy2y_psiL=abs(dLNTy2y_psiL)

eststo: mean dLNTy2y_data
sum abs_dLNTy2y, detail
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(max)

eststo: mean dLNTy2y_bench
sum absdLNTy2y_bench, detail
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(max)

eststo: mean dLNTy2y_tauL
sum absdLNTy2y_tauL, detail
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(max)

eststo: mean dLNTy2y_psiL
sum absdLNTy2y_psiL, detail
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(max)

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 5C, Factor Market Frictions Results: LABOR ALLOCATIONS, year-to-year") /*
 */ append b(3) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("data" "benchmark" "w/o $\tau^L$" /*
 */ "no domestic frictions") /*
 */ stats(p50 p90dLNT maxdLNT N, label("Statistics" "90th \%" /*
 */ "max dLNT" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear




***************
* TABLE 2 - Observed Labor and Value Added Reallocation	
***************

*long-run changes
gen VAN0a=vash_data if year==t_init
gen VANTa=vash_data if year==t_end
bys id: egen VAN0_data=max(VAN0a)
bys id: egen VANT_data=max(VANTa)

drop VAN0a VANTa

gen dVAN_data=(VANT_data-VAN0_data)/TT

eststo: mean dLNT_data
sum dLNT_data, detail
estadd scalar meddLNT=r(p50)
estadd scalar mindLNT=r(min)
estadd scalar maxdLNT=r(max)

eststo: mean dVAN_data
sum dVAN_data, detail
estadd scalar meddLNT=r(p50)
estadd scalar mindLNT=r(min)
estadd scalar maxdLNT=r(max)

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 2A: LABOR REALLOCATIONS") /*
 */ append b(4) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("labor" "value added") /*
 */ stats(meddLNT mindLNT maxdLNT N, label("Median" /*
 */ "minimum" "maximum" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")
eststo clear

*year-to-year changes
eststo: mean abs_dLNTy2y
sum abs_dLNTy2y, detail
estadd scalar meddLNT=r(p50)
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(p99)

eststo: mean abs_dVANTy2y
sum abs_dVANTy2y, detail
estadd scalar meddLNT=r(p50)
estadd scalar p90dLNT=r(p90)
estadd scalar maxdLNT=r(p99)

esttab using "$tablepath\tables_4paper.tex", /*
 */ title("TABLE 2B: LABOR REALLOCATIONS, year-to-year") /*
 */ append b(4) nostar not nonumbers label wide compress obslast varwidth(30) /*
 */ mtitles("labor" "value added") /*
 */ stats(meddLNT p90dLNT maxdLNT N, label("median" "90th \%" /*
 */ "99th \%" "Obs.")) /*
 */ addnotes("any notes:" /*
 */ "another line for notes")

eststo clear


